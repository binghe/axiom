)abbrev package MRF2 MonoidRingFunctions2
++ Author: Johannes Grabmeier
++ Date Created: 14 May 1991
++ Date Last Updated: 14 May 1991
++ Description:
++ MonoidRingFunctions2 implements functions between
++ two monoid rings defined with the same monoid over different rings.

MonoidRingFunctions2(R,S,M) : SIG == CODE where
  R  : Ring
  S  : Ring
  M  : Monoid

  SIG ==> with

    map : (R -> S, MonoidRing(R,M)) -> MonoidRing(S,M)
      ++ map(f,u) maps f onto the coefficients f the element
      ++ u of the monoid ring to create an element of a monoid
      ++ ring with the same monoid b.

  CODE ==> add

      map(fn, u) ==
        res : MonoidRing(S,M) := 0
        for te in terms u repeat
          res := res + monomial(fn(te.coef), te.monom)
        res

