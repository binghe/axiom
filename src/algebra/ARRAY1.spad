)abbrev domain ARRAY1 OneDimensionalArray
++ Author: Mark Botch
++ Description:
++ This is the domain of 1-based one dimensional arrays

OneDimensionalArray(S) : SIG == CODE where
  S : Type

  ARRAYMININDEX ==> 1       -- if you want to change this, be my guest

  SIG ==> OneDimensionalArrayAggregate S with

   oneDimensionalArray : List S -> %
     ++ oneDimensionalArray(l) creates an array from a list of elements l
     ++
     ++X oneDimensionalArray [i**2 for i in 1..10]
  
   oneDimensionalArray : (NonNegativeInteger, S) -> %
     ++ oneDimensionalArray(n,s) creates an array from n copies of element s
     ++
     ++X oneDimensionalArray(10,0.0)

  CODE ==> IndexedOneDimensionalArray(S, ARRAYMININDEX) add

    oneDimensionalArray(u) ==
      n := #u
      n = 0 => empty()
      a := new(n, first u)
      for i in 2..n for x in rest u repeat a.i := x
      a

    oneDimensionalArray(n,s) == new(n,s)

