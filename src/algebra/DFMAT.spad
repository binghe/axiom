)abbrev domain DFMAT DoubleFloatMatrix
++ Author: Waldek Hebisch
++ Description:
++ This is a low-level domain which implements matrices
++ (two dimensional arrays) of double precision floating point
++ numbers.  Indexing is 0 based, there is no bound checking (unless
++ provided by lower level).

DoubleFloatMatrix() : SIG == CODE where

  SIG ==> MatrixCategory(DoubleFloat,
                         DoubleFloatVector,
                         DoubleFloatVector) with

    qnew : (Integer, Integer) -> %
      ++ qnew(n, m) creates a new uninitialized n by m matrix.
      ++
      ++X t1:DFMAT:=qnew(3,4)

  CODE ==> add

    Qelt2 ==> DAREF2$Lisp

    Qsetelt2 ==> DSETAREF2$Lisp

    Qnrows ==> DANROWS$Lisp

    Qncols ==> DANCOLS$Lisp

    Qnew ==> MAKE_-DOUBLE_-MATRIX$Lisp

    Qnew1 ==> MAKE_-DOUBLE_-MATRIX1$Lisp
    
    minRowIndex x == 0

    minColIndex x == 0

    nrows x == Qnrows(x)

    ncols x == Qncols(x)

    maxRowIndex x == Qnrows(x) - 1

    maxColIndex x == Qncols(x) - 1

    qelt(m, i, j) == Qelt2(m, i, j)

    qsetelt_!(m, i, j, r) == Qsetelt2(m, i, j, r)

    empty() == Qnew(0$Integer, 0$Integer)

    qnew(rows, cols) == Qnew(rows, cols)

    new(rows, cols, a) == Qnew1(rows, cols, a)

