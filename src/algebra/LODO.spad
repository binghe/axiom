)abbrev domain LODO LinearOrdinaryDifferentialOperator
++ Author: Manuel Bronstein
++ Date Created: 9 December 1993
++ Date Last Updated: 15 April 1994
++ References:
++ Bron95 On radical solutions of linear ordinary differential equations
++ Abra01 On Solutions of Linear Functional Systems
++ Muld95 Primitives: Orepoly and Lodo
++ Description:
++ \spad{LinearOrdinaryDifferentialOperator} defines a ring of
++ differential operators with coefficients in a ring A with a given
++ derivation.
++ Multiplication of operators corresponds to functional composition:\br
++ \spad{(L1 * L2).(f) = L1 L2 f}

LinearOrdinaryDifferentialOperator(A,diff) : SIG == CODE where
  A : Ring
  diff : A -> A

  SIG ==> LinearOrdinaryDifferentialOperatorCategory A

  CODE ==> SparseUnivariateSkewPolynomial(A, 1, diff) add

        Rep := SparseUnivariateSkewPolynomial(A, 1, diff)

        outputD := "D"@String :: Symbol :: OutputForm

        coerce(l:%):OutputForm == outputForm(l, outputD)

        elt(p:%, a:A):A        == apply(p, 0, a)

        if A has Field then

            import LinearOrdinaryDifferentialOperatorsOps(A, %)

            symmetricProduct(a, b) == symmetricProduct(a, b, diff)

            symmetricPower(a, n)   == symmetricPower(a, n, diff)

            directSum(a, b)        == directSum(a, b, diff)

