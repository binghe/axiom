)abbrev domain MCMPLX MachineComplex
++ Date Created:  December 1993
++ Description: 
++ A domain which models the complex number representation
++ used by machines in the AXIOM-NAG link.

MachineComplex() : SIG == CODE where

  SIG ==> Join (FortranMachineTypeCategory,
                    ComplexCategory(MachineFloat)) with

    coerce : Complex Float -> $
      ++ coerce(u) transforms u into a MachineComplex

    coerce : Complex Integer -> $
      ++ coerce(u) transforms u into a MachineComplex

    coerce : Complex MachineFloat -> $
      ++ coerce(u) transforms u into a MachineComplex

    coerce : Complex MachineInteger -> $
      ++ coerce(u) transforms u into a MachineComplex

    coerce : $ -> Complex Float
      ++ coerce(u) transforms u into a COmplex Float

  CODE ==> Complex MachineFloat add

    coerce(u:Complex Float):$ == 
      complex(real(u)::MachineFloat,imag(u)::MachineFloat)

    coerce(u:Complex Integer):$ ==
      complex(real(u)::MachineFloat,imag(u)::MachineFloat)

    coerce(u:Complex MachineInteger):$ ==
      complex(real(u)::MachineFloat,imag(u)::MachineFloat)

    coerce(u:Complex MachineFloat):$ == 
      complex(real(u),imag(u))

    coerce(u:$):Complex Float ==
      complex(real(u)::Float,imag(u)::Float)

