)abbrev category ORDSET OrderedSet
++ Description:
++ The class of totally ordered sets, that is, sets such that for each 
++ pair of elements \spad{(a,b)}
++ exactly one of the following relations holds \spad{a<b or a=b or b<a}
++ and the relation is transitive, that is, \spad{a<b and b<c => a<c}.

OrderedSet() : Category == SIG where

  SIG ==> SetCategory with

    "<" : (%,%) -> Boolean
      ++ x < y is a strict total ordering on the elements of the set.

    ">" : (%, %) -> Boolean
      ++ x > y is a greater than test.

    ">=" : (%, %) -> Boolean
      ++ x >= y is a greater than or equal test.

    "<=" : (%, %) -> Boolean
      ++ x <= y is a less than or equal test.

    max : (%,%) -> %
      ++ max(x,y) returns the maximum of x and y relative to "<".

    min : (%,%) -> %
      ++ min(x,y) returns the minimum of x and y relative to "<".

   add

      x,y: %
  
      -- These really ought to become some sort of macro
  
      max(x,y) ==
        x > y => x
        y
  
      min(x,y) ==
        x > y => y
        x
  
      ((x: %) >  (y: %)) : Boolean == y < x
  
      ((x: %) >= (y: %)) : Boolean == not (x < y)
  
      ((x: %) <= (y: %)) : Boolean == not (y < x)

