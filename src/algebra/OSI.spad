)abbrev domain OSI OrdSetInts
++ Author: Larry Lambe
++ Date created: 14 August 1988
++ Date Last Updated: 11 March 1991
++ Description: 
++ A domain used in order to take the free R-module on the
++ Integers I.  This is actually the forgetful functor from OrderedRings
++ to OrderedSets applied to I

OrdSetInts() : SIG == CODE where

  I  ==> Integer
  L  ==> List
  O  ==> OutputForm

  SIG ==> OrderedSet with

     coerce : Integer -> %
       ++ coerce(i) returns the element corresponding to i

     value : % -> I
       ++ value(x) returns the integer associated with x

  CODE ==> add

     Rep := Integer

     x,y: %

     x = y == x =$Rep y

     x < y == x <$Rep y

     coerce(i:Integer):% == i

     value(x) == x:Rep

     coerce(x):O ==
       sub(e::Symbol::O, coerce(x)$Rep)$O

