)abbrev domain DFVEC DoubleFloatVector
++ Author: Waldek Hebisch
++ Description:
++ This is a low-level domain which implements vectors
++ (one dimensional arrays) of double precision floating point
++ numbers.  Indexing is 0 based, there is no bound checking (unless
++ provided by lower level).

DoubleFloatVector() : SIG == CODE where

  SIG ==> VectorCategory DoubleFloat with

    qnew : Integer -> %
      ++ qnew(n) creates a new uninitialized vector of length n.
      ++
      ++X t1:DFVEC:=qnew(7)

  CODE ==> add
    
    Qelt1 ==> DELT$Lisp

    Qsetelt1 ==> DSETELT$Lisp

    qelt(x, i) == Qelt1(x, i)

    qsetelt_!(x, i, s) == Qsetelt1(x, i, s)

    Qsize ==> DLEN$Lisp

    Qnew ==> MAKE_-DOUBLE_-VECTOR$Lisp

    Qnew1 ==> MAKE_-DOUBLE_-VECTOR1$Lisp

    #x                          == Qsize x

    minIndex x                  == 0

    empty()                     == Qnew(0$Lisp)

    qnew(n)                     == Qnew(n)

    new(n, x)                   == Qnew1(n, x)

    elt(x:%, i:Integer)         == Qelt1(x, i)

    setelt(x : %, i : Integer, s : DoubleFloat) == Qsetelt1(x, i, s)

    fill_!(x, s)       ==
        for i in 0..((Qsize(x)) - 1) repeat Qsetelt1(x, i, s)
        x

