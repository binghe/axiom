)abbrev package UPOLYC2 UnivariatePolynomialCategoryFunctions2
++ Description:
++ Mapping from polynomials over R to polynomials over S
++ given a map from R to S assumed to send zero to zero.

UnivariatePolynomialCategoryFunctions2(R,PR,S,PS) : SIG == CODE where
  R : Ring
  PR : UnivariatePolynomialCategory R
  S : Ring
  PS : UnivariatePolynomialCategory S

  SIG ==> with

    map : (R -> S, PR) -> PS
      ++ map(f, p) takes a function f from R to S,
      ++ and applies it to each (non-zero) coefficient of a polynomial p
      ++ over R, getting a new polynomial over S.
      ++ Note that since the map is not applied to zero elements, it may map 
      ++ zero to zero.

  CODE ==> add

    map(f, p) ==
      ans:PS := 0
      while p ^= 0 repeat
        ans := ans + monomial(f leadingCoefficient p, degree p)
        p   := reductum p
      ans

