)abbrev package MAPPKG2 MappingPackage2
++ Description: 
++ Various Currying operations.

MappingPackage2(A,C) : SIG == CODE where
  A : SetCategory
  C : SetCategory

  NNI ==> NonNegativeInteger
 
  SIG ==> with

    const : C -> (A ->C)
      ++\spad{const c} is a function which produces \spad{c} when
      ++ applied to its argument.
 
    curry : (A ->C, A) -> (()->C)
      ++\spad{cu(f,a)} is the function \spad{g}
      ++ such that \spad{g ()= f a}.

    constant : (()->C) -> (A ->C)
      ++\spad{vu(f)} is the function \spad{g}
      ++ such that \spad{g a= f ()}.
 
    diag : ((A,A)->C) -> (A->C)
      ++\spad{diag(f)} is the function \spad{g}
      ++ such that \spad{g a = f(a,a)}.
 
  CODE ==> add
 
    MappingPackageInternalHacks2(A, C)
 
    a: A
    c: C
    faa:  A -> A
    f0c:  ()-> C
    fac:  A -> C
    faac: (A,A)->C
 
    const c == (a1:A):C +-> arg2(a1, c)

    curry(fac, a) == fac a

    constant f0c == (a1:A):C +-> arg2(a1, f0c())

    diag faac == (a1:A):C +-> faac(a1, a1)

