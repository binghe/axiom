)abbrev package CARTEN2 CartesianTensorFunctions2
++ Author: Stephen M. Watt
++ Date Created: December 1986
++ Date Last Updated: May 30, 1991
++ Description:
++ This package provides functions to enable conversion of tensors
++ given conversion of the components.

CartesianTensorFunctions2(minix, dim, S, T) : SIG == CODE where
  minix : Integer
  dim : NonNegativeInteger
  S : CommutativeRing
  T : CommutativeRing

  CS  ==> CartesianTensor(minix, dim, S)
  CT  ==> CartesianTensor(minix, dim, T)

  SIG ==> with

    reshape : (List T, CS) -> CT
      ++ reshape(lt,ts) organizes the list of components lt into
      ++ a tensor with the same shape as ts.

    map : (S->T,   CS) -> CT
      ++ map(f,ts) does a componentwise conversion of the tensor ts
      ++ to a tensor with components of type T.

  CODE ==> add

      reshape(l, s) == unravel l

      map(f, s) == unravel [f e for e in ravel s]

